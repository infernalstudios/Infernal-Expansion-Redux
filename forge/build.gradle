plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at)
    }

    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }

    runs {
        client {
            client()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
            sourceSet project(":common").sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.2-1'
    compileOnly "io.github.llamalad7:mixinextras-common:${mixinextras_version}"
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:${mixinextras_version}"))
    annotationProcessor "io.github.llamalad7:mixinextras-common:${mixinextras_version}"

    // EMI
    modImplementation "dev.emi:emi-forge:${emi_version}"

    // Cloth Config
    modImplementation "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"

    // GeckoLib
    modImplementation "software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}"

    // TerraBlender
    modCompileOnly "com.github.glitchfiend:TerraBlender-forge:${terrablender_version}"

    // Abnormals
    modCompileOnly "maven.modrinth:autumnity:${autumnity_version}"
    modCompileOnly "maven.modrinth:environmental:${environmental_version}"
    modCompileOnly "maven.modrinth:caverns-and-chasms:${caverns_and_chasms_version}"
    modCompileOnly "maven.modrinth:blueprint:${blueprint_version}"

    // Jaden's Nether Expansion
    modCompileOnly "maven.modrinth:jadens-nether-expansion:${jne_version}-Forge"

    compileOnly project(":common")
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
}

jar {
    finalizedBy('reobfJar')
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    ])
}

publishMods {
    file = tasks.named("reobfJar").flatMap { it.archiveFile }
    changelog = rootProject.file("CHANGELOG-LATEST.md").text
    type = STABLE
    displayName = "${mod_name} Forge ${minecraft_version} - ${project.version}"
    modLoaders.add("forge")

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "1407992"
        minecraftVersions.add("${minecraft_version}")

        requires { slug = "geckolib" }
        optional { slug = "lambdynamiclights" }
        optional { slug = "cloth-config" }
        optional { slug = "afterimages" }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "wiPmcmNp"
        minecraftVersions.add("${minecraft_version}")

        requires { slug = "geckolib" }
        optional { slug = "lambdynamiclights" }
        optional { slug = "cloth-config" }
        optional { slug = "afterimages" }
    }
}