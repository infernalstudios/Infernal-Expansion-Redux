plugins {
    id 'multiloader-common'
    id 'me.modmuss50.mod-publish-plugin'
}

configurations {
    commonJava {
        canBeResolved = true
    }
    commonResources {
        canBeResolved = true
    }
}

dependencies {
    compileOnly(project(':common')) {
        capabilities {
            requireCapability "$group:$mod_id"
        }
    }
    commonJava project(path: ':common', configuration: 'commonJava')
    commonResources project(path: ':common', configuration: 'commonResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

processResources {
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
}

publishMods {
    changelog = rootProject.file("CHANGELOG-LATEST.md").text
    type = STABLE
    displayName = "${mod_name} ${project.name.capitalize()} ${minecraft_version} - ${project.version}"
    modLoaders.add(project.name)

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "1407992"
        minecraftVersions.add("${minecraft_version}")

        requires { slug = "geckolib" }
        optional { slug = "lambdynamiclights" }
        optional { slug = "cloth-config" }
        optional { slug = "afterimages" }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "wiPmcmNp"
        minecraftVersions.add("${minecraft_version}")

        requires { slug = "geckolib" }
        optional { slug = "lambdynamiclights" }
        optional { slug = "cloth-config" }
        optional { slug = "afterimages" }
    }
}