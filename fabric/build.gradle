plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // EMI
    modRuntimeOnly "dev.emi:emi-fabric:${emi_version}"

    // Cloth Config
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"
    modImplementation "com.terraformersmc:modmenu:${mod_menu_version}"

    // TerraBlender
    modCompileOnly "com.github.glitchfiend:TerraBlender-fabric:$terrablender_version"

    // GeckoLib
    modImplementation "software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}"

    // Jaden's Nether Expansion
    modCompileOnly "maven.modrinth:forge-config-api-port:${fcapi_version}"
    modCompileOnly "maven.modrinth:architectury-api:${architectury_version}+fabric"
    modCompileOnly "maven.modrinth:jadens-nether-expansion:${jne_version}-Fabric"

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
        datagen {
            client()
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"

            vmArg "-Dfabric-api.datagen.output-dir=${project(":common").file("src/main/generated")}"

            ideConfigGenerated(true)
            runDir("build/datagen")
        }
    }
}

publishMods {
    file = tasks.named("remapJar").flatMap { it.archiveFile }
    changelog = rootProject.file("CHANGELOG.md").text
    type = STABLE
    displayName = "${mod_name} Fabric ${minecraft_version} - ${project.version}"
    modLoaders.add("fabric")

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "1407992"
        minecraftVersions.add("1.20.1")

        requires { slug = "geckolib" }
        optional { slug = "lambdynamiclights" }
        optional { slug = "cloth-config" }
        optional { slug = "afterimages" }
    }

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "wiPmcmNp"
        minecraftVersions.add("1.20.1")

        requires { slug = "geckolib" }
        optional { slug = "lambdynamiclights" }
        optional { slug = "cloth-config" }
        optional { slug = "afterimages" }
    }
}