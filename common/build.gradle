plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev.legacyforge'
}

legacyForge {
    mcpVersion = minecraft_version
    if (file("src/main/resources/META-INF/accesstransformer.cfg").exists()) {
        accessTransformers = ["src/main/resources/META-INF/accesstransformer.cfg"]
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnly group: 'org.ow2.asm', name: 'asm-tree', version: '9.6'
    compileOnly "io.github.llamalad7:mixinextras-common:${mixinextras_version}"
    annotationProcessor "io.github.llamalad7:mixinextras-common:${mixinextras_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    compileOnly "dev.emi:emi-xplat-mojmap:${emi_version}"
    compileOnly "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"
    compileOnly "com.github.glitchfiend:TerraBlender-common:$terrablender_version"
    compileOnly "software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}"
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}
